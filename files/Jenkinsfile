library identifier: 'ChimpDelivery-JenkinsLibrary@master', retriever: modernSCM([
    $class: 'GitSCMSource',
    credentialsId: 'GIT_ACCOUNT',
    remote: 'https://github.com/ChimpDelivery/ChimpDelivery-JenkinsLibrary.git'
])

pipeline
{
    agent { label 'WINDOWS || MAC' }
    
    options 
    {
        disableConcurrentBuilds()
    }
    
    parameters
    {
        booleanParam(name: "INVOKE_PARAMETERS", defaultValue: true, description: "Do you wish to do a Dry Run?")
        choice(name: "PLATFORM", choices: ['Appstore', 'GooglePlay'], description: "Deliver Platform")
        string(name: "APP_ID", defaultValue: "-1", description: "Dashboard App ID")
        string(name: "STORE_BUILD_VERSION", defaultValue: "1.0", description: "Store Application Version")
        booleanParam(name: "STORE_CUSTOM_BUNDLE_VERSION", defaultValue: false, description: "Do you set custom app build number?")
        string(name: "STORE_BUNDLE_VERSION", defaultValue: "1", description: "Store Bundle Version")
        string(name: "DASHBOARD_PROFILE_UUID", defaultValue: "DASHBOARD_DEFAULT_PROFILE_UUID", description: "iOS Provision Profile UUID")
        string(name: "DASHBOARD_TEAM_ID", defaultValue: "DASHBOARD_DEFAULT_TEAM_ID", description: "iOS Provision Profile Team ID")
        string(name: "UNITY_VERSION", defaultValue: "2021.3.5f1", description: "Target Unity3D Version")
    }

    environment
    {
        // jenkins can be in external storage
        TOP_ROOT = '/Volumes/Mac'
        
        // dashboard auth
        DASHBOARD_ACCOUNT = credentials('DASHBOARD_ACCOUNT')

        // notification settings
        DISCORD_WEBHOOK_URL = credentials('DISCORD_WEBHOOK_URL') // (channel->settings->integrations)

        // unity3d settings
        UNITY_PATH = getUnityPath(unityVersion: params.UNITY_VERSION)
        UNITY_USERNAME = credentials('UNITY_USERNAME')
        UNITY_PASSWORD = credentials('UNITY_PASSWORD')
        UNITY_SERIAL = credentials('UNITY_SERIAL')

        //
        CONVERTED_PLATFORM = "${params.PLATFORM == 'Appstore' ? 'iOS' : 'Android' }"
        
        // unity execution points
        UNITY_ASSET_METHOD = "TalusCI.Editor.SettingProviders.${CONVERTED_PLATFORM}.Runner.Run"
        UNITY_BUILD_METHOD = "TalusCI.Editor.BuildSystem.BuildActions.${CONVERTED_PLATFORM}Release"

        // store version
        STORE_VERSION = "${params.STORE_CUSTOM_BUNDLE_VERSION == true ? params.STORE_BUNDLE_VERSION : BUILD_NUMBER}"

        // ioS
        TESTFLIGHT_USER_ACCOUNT = credentials('TF_USER_ACCOUNT')

        // android
        ANDROID_NDK_ROOT = "/Applications/Unity/Hub/Editor/${UNITY_VERSION}/PlaybackEngines/AndroidPlayer/NDK"
        ANDROID_KEYSTORE = credentials('ANDROID_KEYSTORE')
        
        // ios-signing
        KEYCHAIN_LOCATION = new URI([ env.TOP_ROOT, env.HOME, 'Library/Keychains/login.keychain' ].join('/')).normalize().toString()
        KEYCHAIN_PASS = credentials('KEYCHAIN_PASSWORD')
    }

    stages
    {
        stage('Prepare')
        {
            when { anyOf {
                expression { params.INVOKE_PARAMETERS == true }
                expression { params.APP_ID == "-1" }
            } }

            steps { parameterizeJob() }
        }

        stage('Version')
        {
            steps { runVersionStage() }
        }

        stage('UnityBuild')
        {
            steps { runUnityBuildStage() }
        }

        stage('iOS-Sign')
        {
            when { expression { params.PLATFORM == 'Appstore' } }
            steps { runXcodeSignStage() }
        }

        stage('iOS-Archieve')
        {
            when { expression { params.PLATFORM == 'Appstore' } }
            steps { runXcodeArchiveStage() }
        }

        stage('iOS-Ipa')
        {
            when { expression { params.PLATFORM == 'Appstore' } }
            steps { runXcodeIpaStage() }
        }

        stage('Upload')
        {
            options { timeout(time: 30, unit: 'MINUTES') }
            steps { runUploadStage() }
        }
    }

    post
    {
        always { notifyDiscord() }
        cleanup { clearProjectArtifacts() }
    }
}
